// Copyright (C) 2025 Zac Sweers
// SPDX-License-Identifier: Apache-2.0
import com.vanniktech.maven.publish.DeploymentValidation
import com.vanniktech.maven.publish.MavenPublishBaseExtension
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.jvm.toolchain.JavaLanguageVersion
import org.gradle.jvm.toolchain.JavaToolchainService
import org.gradle.kotlin.dsl.withType
import org.jetbrains.dokka.gradle.DokkaExtension
import org.jetbrains.dokka.gradle.engine.parameters.VisibilityModifier
import org.jetbrains.kotlin.gradle.dsl.JvmDefaultMode
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompilerOptions
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.plugin.KotlinBasePlugin
import org.jetbrains.kotlin.gradle.targets.js.testing.KotlinJsTest
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

val catalog = rootProject.extensions.getByType<VersionCatalogsExtension>().named("libs")
val jdkVersion = catalog.findVersion("jdk").get().requiredVersion
val jvmTargetVersion = catalog.findVersion("jvmTarget").get().requiredVersion
val compilerJvmTargetVersion = catalog.findVersion("compilerJvmTarget").get().requiredVersion

val metroExtension =
  project.extensions.create<MetroProjectExtension>("metroProject").apply {
    jvmTarget.convention(
      if (isCompilerProject) {
        compilerJvmTargetVersion
      } else {
        jvmTargetVersion
      }
    )
  }

// Java configuration
pluginManager.withPlugin("java") {
  extensions.configure<JavaPluginExtension> {
    toolchain { languageVersion.convention(JavaLanguageVersion.of(jdkVersion)) }
  }
  tasks.withType<JavaCompile>().configureEach {
    options.release.convention(metroExtension.jvmTarget.map(String::toInt))
  }
}

// Suppress native access warnings and ReservedStackAccess warnings in forked JVMs
val ciVerbose = providers.gradleProperty("metro.ciVerbose").map { it.toBoolean() }.orElse(false)
val configuredTestJavaVersion = providers.gradleProperty("metro.testJavaVersion").map(String::toInt)
val effectiveTestJavaVersion = configuredTestJavaVersion.orElse(jdkVersion.toInt())

pluginManager.withPlugin("java-base") {
  if (configuredTestJavaVersion.isPresent) {
    val javaToolchains = extensions.getByType<JavaToolchainService>()
    tasks.withType<Test>().configureEach {
      javaLauncher.set(
        javaToolchains.launcherFor {
          languageVersion.set(configuredTestJavaVersion.map(JavaLanguageVersion::of))
        }
      )
    }
  }
}

tasks.withType<Test>().configureEach {
  jvmArgs("--enable-native-access=ALL-UNNAMED")
  if (effectiveTestJavaVersion.get() >= 23) {
    jvmArgs("--sun-misc-unsafe-memory-access=allow")
  }
  jvmArgs("-XX:StackReservedPages=0")

  // metro.ciVerbose surfaces test-lifecycle events (started/passed/skipped/failed) and stdout/err
  // so CI logs show in-flight test progress instead of just batch-completion summaries. Useful
  // for diagnosing test hangs where the build dies inside a single test without ever printing a
  // result line.
  if (ciVerbose.get()) {
    testLogging {
      events("started", "passed", "skipped", "failed")
      showStandardStreams = true
      showStackTraces = true
      showCauses = true
      exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
      displayGranularity = 0
    }
  }
}

tasks.withType<JavaExec>().configureEach {
  jvmArgs(
    "--enable-native-access=ALL-UNNAMED",
    "--sun-misc-unsafe-memory-access=allow",
    "-XX:StackReservedPages=0",
  )
}

// Kotlin configuration
plugins.withType<KotlinBasePlugin> {
  tasks.withType<KotlinCompilationTask<*>>().configureEach {
    compilerOptions {
      progressiveMode.convention(metroExtension.progressiveMode)
      languageVersion.convention(metroExtension.languageVersion)
      apiVersion.convention(metroExtension.apiVersion)
      if (this is KotlinJvmCompilerOptions) {
        jvmTarget.convention(metroExtension.jvmTarget.map(JvmTarget::fromTarget))
        jvmDefault.convention(JvmDefaultMode.NO_COMPATIBILITY)
        freeCompilerArgs.add("-Xassertions=jvm")
        if (isCompilerProject) {
          freeCompilerArgs.addAll(
            "-Xreturn-value-checker=full",
            "-Xcontext-sensitive-resolution",
            "-Xwhen-expressions=indy",
            //  "-Xallow-contracts-on-more-functions",
            //  "-Xallow-condition-implies-returns-contracts",
            //  "-Xallow-holdsin-contract",
          )
          optIn.addAll(
            "kotlin.contracts.ExperimentalContracts",
            "kotlin.contracts.ExperimentalExtendedContracts",
            "org.jetbrains.kotlin.compiler.plugin.ExperimentalCompilerApi",
            "org.jetbrains.kotlin.ir.symbols.UnsafeDuringIrConstructionAPI",
          )
        }
      }
    }
  }
}

pluginManager.withPlugin("org.jetbrains.kotlin.multiplatform") {
  // Suppress "WASI is an experimental feature" Node.js warnings
  tasks.withType<KotlinJsTest>().configureEach {
    if (name.contains("wasmWasi", ignoreCase = true)) {
      nodeJsArgs += "--no-warnings"
    }
  }
}

pluginManager.withPlugin("metro.publish") {
  val metroArtifact = extensions.getByType<MetroArtifactExtension>()
  tasks.withType<KotlinCompilationTask<*>>().configureEach {
    compilerOptions {
      if (this is KotlinJvmCompilerOptions) {
        // Configuration required to produce unique META-INF/*.kotlin_module file names
        moduleName.set(metroArtifact.artifactId)
      }
    }
  }

  val isNotCompiler = project.path != ":compiler" && !project.path.startsWith(":compiler-compat")

  if (isNotCompiler) {
    val metroRuntimeLanguageVersion =
      catalog.findVersion("kotlinPublished").get().requiredVersion.take(3) // Take 2.2 out of 2.2.20
    val runtimeKotlinVersion = KotlinVersion.fromVersion(metroRuntimeLanguageVersion)
    metroExtension.languageVersion.convention(runtimeKotlinVersion)
    metroExtension.apiVersion.convention(runtimeKotlinVersion)
    if (runtimeKotlinVersion < KotlinVersion.DEFAULT) {
      metroExtension.progressiveMode.set(false)
    }
  }

  // Maven publish configuration
  plugins.withId("com.vanniktech.maven.publish") {
    // Apply dokka for non-compiler projects
    if (isNotCompiler) {
      apply(plugin = "org.jetbrains.dokka")
    }

    extensions.configure<MavenPublishBaseExtension> {
      publishToMavenCentral(
        automaticRelease = true,
        validateDeployment = DeploymentValidation.VALIDATED,
      )
    }
  }
}

// Android configuration
pluginManager.withPlugin("com.android.library") { apply(plugin = "metro.android") }

pluginManager.withPlugin("com.android.application") { apply(plugin = "metro.android") }

// Dokka configuration
pluginManager.withPlugin("org.jetbrains.dokka") {
  extensions.configure<DokkaExtension> {
    basePublicationsDirectory.convention(layout.buildDirectory.dir("dokkaDir"))
    dokkaSourceSets.configureEach {
      skipDeprecated.convention(true)
      documentedVisibilities.add(VisibilityModifier.Public)
      reportUndocumented.convention(true)
      perPackageOption {
        matchingRegex.set(".*\\.internal.*")
        suppress.set(true)
      }
      sourceLink {
        localDirectory.convention(layout.projectDirectory.dir("src"))
        val relPath = rootProject.projectDir.toPath().relativize(projectDir.toPath())
        remoteUrl(
          providers.gradleProperty("POM_SCM_URL").map { scmUrl -> "$scmUrl/tree/main/$relPath" }
        )
        remoteLineSuffix.convention("#L")
      }
    }
  }
}
